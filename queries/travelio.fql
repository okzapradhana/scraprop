LET url = "https://www.travelio.com/sewa-apartemen-jakarta"

LET travelio = DOCUMENT(url, {
    driver: "cdp"
})

/*pagination*/
LET itemPerPageSelector = "div#hotel-pagination > div > div#showing-hotel > span:nth-child(1)"
LET maxItemPageSelector = "div#hotel-pagination > div > div#showing-hotel > span:nth-child(2)"
LET maxItemPage = ELEMENT(travelio, maxItemPageSelector)
LET itemPerPage = ELEMENT(travelio, itemPerPageSelector)
LET splitItemPerPage  = TO_INT(SPLIT(itemPerPage.innerText, " - ")[1])
LET maxPages = TO_INT(maxItemPage.innerText)
LET pagination = ROUND(maxPages/splitItemPerPage)

LET result = (
    FOR pageNum IN 1..pagination
        PRINT("Curr page", pageNum)
        /*LIMIT 2*/
        FILTER ELEMENT_EXISTS(travelio, "div.property-outer-box") == true

        LET currentPage = pageNum == 1 ? true : NAVIGATE(travelio, (url+"?page="+pageNum), 20000)
        LET properties = ELEMENTS(travelio, ".property-box")
        PRINT("is Curr Page == 1?", currentPage)
        PRINT("aparts", LENGTH(properties))
        LET links = (
            FOR property IN properties
                LET href = ELEMENT(property, "a")
                RETURN href.attributes.href
        )
        LET items = (
            FOR link IN links
                /*LIMIT 2*/
                PRINT("Curr link", link)

                NAVIGATE(travelio, CONCAT("https:", link), 20000)
                WAIT_ELEMENT(travelio, "#page-body-wrapper")
                LET apartName = ELEMENT(travelio, "#hotel-name")
                LET kamar = ELEMENTS(travelio, "div#page-body-left > div:nth-child(6) div.hotel-left-item-info")
                LET property = ELEMENTS(travelio, "div#page-body-left > div:nth-child(7) > div.hotel-left-item-info-wrapper > div.hotel-left-item-info")
                LET facilities = ELEMENTS(travelio, "div#page-body-left > div:nth-child(7) div.hotel-facility-item")
                LET pricesLabelSelector = ELEMENTS(travelio, "ul.nav-tabs > li")
                LET lengthPrices = LENGTH(pricesLabelSelector)

                LET pricesArray = (
                    FOR eachPrice IN 1..lengthPrices
                        LET tabPriceSelector = "ul.nav-tabs > li:nth-child(" + eachPrice + ")" + " > a"
                        LET tabPrice = ELEMENT(travelio, tabPriceSelector)
                        CLICK(travelio, tabPriceSelector)
                        PRINT("Tab price: ", tabPrice.innerText)
                        WAIT(10000)

                        LET isPriceNotExist = ELEMENT(travelio, "div#room-price-detail > span.price-value").innerText == "-"
                        PRINT("is price not exist?", isPriceNotExist)
                        LET time = ELEMENT(travelio, "ul.nav-tabs > li:nth-child(" + eachPrice + ")" + " > a")
                        /*
                            Check total price, 
                            It returns the total price with any condition, although the bedroom has been
                            rent by someone (the price is -). It checks many conditions.

                            Conditions:
                            If current tab == "Tahunan" and the price is not exist,
                                It will return "Kamar Sudah Tersewa"
                            If current tab == "Tahunan" but the price is exist, 
                                Click the dropdown to choose payment options. 
                                Then the total price will exist.
                            If current tab is not "Tahunan" (means "Harian" or "Bulanan") and the price is not exist. 
                                It will return "Kamar Sudah Tersewa"
                            If current tab is not "Tahunan" but the price is exist,
                                Get the Total Price directly.
                        */
                        LET isTahun = tabPrice.innerText == "Tahunan"
                        PRINT("is current Tab = Tahunan?",  isTahun)
                        LET clickDropdown = isTahun == true ? (isPriceNotExist == true ? '-' : CLICK(travelio, "select#installment-yearly")) : "No Dropdown"
                        PRINT("Dropdown?", clickDropdown)
                        LET choosePayment = clickDropdown != "No Dropdown" ? CLICK(travelio, "select#installment-yearly > option:nth-child(2)") : "No Payment"
                        LET priceTahun =  choosePayment != "No Payment" ? ELEMENT(travelio, "div.price-total-name").innerText : "No Price Tahun"
                        LET priceExceptTahun = isTahun == false && isPriceNotExist == true ? '-' : ELEMENT(travelio, "div.price-total-name").innerText
                        LET priceTotalLabel = isTahun == true ? priceTahun : priceExceptTahun 
                        LET priceTotal = ELEMENT(travelio, "div#room-price-detail > span.price-value")
                        LET priceText = isPriceNotExist == false ? + "{" + time.innerText + ":{" + priceTotalLabel + ":" + priceTotal.innerText + "}}" : "{" + time.innerText + ": Kamar Sudah Tersewa" + "}"
                        RETURN priceText
                )

                LET kamarFacility = (
                    FOR eachKmr IN kamar
                        RETURN TRIM(eachKmr.innerText)
                )


                LET propertyFacility = (
                    FOR eachProp IN property
                        RETURN TRIM(eachProp.innerText)
                )

                LET allFacility = (
                    FOR eachFac IN facilities
                        RETURN TRIM(eachFac.innerText)
                )

                LET info = INNER_TEXT(travelio, "div#page-body-left > div:nth-child(8)")


                RETURN{
                    nama: apartName.innerText,
                    kamar: kamarFacility,
                    properti: propertyFacility,
                    fasilitas: allFacility,
                    harga: pricesArray,
                    info
                }
        )
        

        RETURN items
)

RETURN FLATTEN(result)
